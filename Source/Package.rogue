uses Utility/Best

try
  forEach (type in TypeInfo.iterator)
    if (type.instance_of(<<Package>>) and type is not <<Package>>)
      local package = type.create_object<<Package>>
      package.init( System.command_line_arguments.cloned )
      package.handle_install
    endIf
  endForEach

catch (err:Error)
  local w = Console.width.or_smaller(80)
  local hr = "=" * w
  Console.error.println hr
  Console.error.println "ERROR\n  "
  Console.error.println err->String.indented(2)
  Console.error.println hr
  System.exit 1

endTry

class Package
  PROPERTIES
    app_name         : String

    version          : String
    url              : String
    pls_home         : String
    launcher_folder  : String
    install_folder   : String
    bin_folder       : String
    archive_filename : String
    archive_folder   : String
    is_unpacked      : String

    releases         = @[]
    properties       : Value

  METHODS
    method init( args:String[] )
      require args.count
      properties = JSON.parse( args.first )
      app_name = properties//app_name
      pls_home = properties//pls_home
      launcher_folder = pls_home/"bin"

      init
      select_version
      install_folder = "$/$" (properties//package_folder,version)
      bin_folder = install_folder/"bin"

    method init
      noAction

    method build
      # Attempts to automatically figure out how build the downloaded & unpacked
      # archive and launches the appropriate commands.
      forEach (folder in File.listing(".",&folders))
        if (File.exists(folder/"Build.rogue"))
          execute "cd $ && rogo build" (folder)
          archive_folder = folder
          return
        endIf
      endForEach

      if (not is_unpacked)
        # Most likely the script just didn't call unpack() yet.
        unpack
        is_unpacked = true
        build
      else
        throw error( "Unable to determine how to build $ after unpacking it."(url) )
      endIf

    method copy_executable( src_filepath:String, dest_filename=null:String )
      if (not dest_filename) dest_filename = app_name
      local dest_filepath = bin_folder/dest_filename

      println "Copying $ -> $" (src_filepath,dest_filepath)
      File.copy( src_filepath, dest_filepath )
      execute( "chmod u+x $" (File.esc(dest_filepath)) )

    method create_folder( folder:String )
      println "  " + folder
      File.create_folder( folder )
      if (not File.is_folder(folder))
        throw error( ''Error creating folder "$".''(folder) )
      endIf

    method download->String
      execute "curl -LfsSO " + url
      archive_filename = File.filename(url)
      if (not File.exists(archive_filename)) throw error( "Error downloading " + url )
      return archive_filename

    method error( message:String )->Error
      return Error( "[$] $"(app_name,message) )

    method execute( cmd:String )
      println "> " + cmd
      if (0 != System.run(cmd))
        throw error( "Error executing:\n"+cmd )
      endIf

    method handle_install
      println "Installing $ v$" (app_name,version)
      println "Creating install folder"
      create_folder( install_folder )
      create_folder( bin_folder )
      install

    method install
      throw error( "install() must overridden." )

    method link
      local exe_list = File.listing( bin_folder/"*" )
      forEach (exe in exe_list)
        local launcher = launcher_folder/File.filename(exe)
        if (not File.exists(launcher))
          local cmd = "ln -s $ $" (exe,launcher)
          execute cmd
        endIf
      endForEach

    method release( url:String, platforms=null:String, version=null:String )
      # Registers a release with .tar.gz/.zip URL and version number.
      #
      # platforms
      #   Any combination of characters w, m, l (windows, mac, linux)
      #   If unspecified then .tar.gz implies "ml" and any other extension
      #   implies "wml". Mac and Linux will prefer .tar.gz over other extensions
      #   if multiple possibilities are given.
      #
      # version
      #   In the format "1", "1.0", "1.0.0", etc. Will be inferred from url
      #   if unspecified.
      if (not platforms)
        local lc = url.to_lowercase
        platforms = which { lc.ends_with(".tar.gz"):"ml" || "wml" }
      endIf

      if (not version)
        local lc = url.to_lowercase
        local span = lc.locate_pattern("v$(I)")
        contingent
          sufficient (span.exists)
          span = lc.locate_pattern("$(I).(I)")
          sufficient (span.exists)
          span = lc.locate_pattern("$(I)?(I)")
          sufficient (span.exists)
          span = lc.locate_pattern("$(I)")
          sufficient (span.exists)
          throw error( 'Cannot determine version number from release URL "$".'(url) )
        endContingent
        version = url.substring( span.value.index )
        local separator_index = span.value.index + span.value.count
        local separator = which{ (separator_index<url.count):url[separator_index] || '.' }
        use builder = StringBuilder.pool
          local found_numbers = false
          forEach (ch in version)
            if (ch == separator) builder.print "."
            elseIf (ch.is_number) builder.print ch; found_numbers = true
            elseIf (found_numbers) escapeForEach
          endForEach
          version = builder->String.without_trailing('.')
        endUse
      endIf

      releases.add @{ :version, :url, :platforms }

    method select_version( version=null )
      if (releases.is_empty) throw error( "No releases are available." )

      if (String.exists(version))
        local best = Best<<String>>( (a,b) => VersionNumber(a) > b )
        forEach (release in releases)
          local v = release//version->String
          trace version, v, VersionNumber(version).is_compatible_with(v)
          if (VersionNumber(version).is_compatible_with(v)) best.consider( v )
        endForEach
        if (not best.exists) throw error( "No such release '$'."(version) )
        version = best.value

      else
        local best = Best<<String>>( (a,b) => VersionNumber(a) > b )
        forEach (release in releases)
          local v = release//version->String
          best.consider( v )
        endForEach
        version = best.value

      endIf

      # Now pick the best URL with the given version number
      url = null
      local platform = which{ System.is_windows:'w' || System.is_macos:'m' || 'l' }
      forEach (release in releases)
        if (VersionNumber(release//version) == version and release//platforms->String.contains(platform))
          url = release//url
          if (not System.is_windows and url.ends_with(".tar.gz",&ignore_case)) escapeForEach
        endIf
      endForEach

    method unpack
      if (not archive_filename or not File.exists(archive_filename))
        download
      endIf
      if (archive_filename.ends_with(".zip",&ignore_case))
        throw error( "TODO: unpack ZIP." )
      elseIf (archive_filename.ends_with(".tar.gz"))
        execute "tar -xvf " + archive_filename
        is_unpacked = true
      else
        throw error( "Cannot unpack() file type '.$'; write custom install() code to handle it." )
      endIf
endClass

#{
class Package
  PROPERTIES
    package  : String
      # E.g. https://github.com/user/repo/.../app      [or]
      #          git@github.com:user/repo.git/.../app
    repo     : String
      # E.g. https://github.com/user/repo
      #          git@github.com:user/repo.git
    app      : String
      # E.g. .../app

    homepage : String
    version  : String
    url      : String
    sha256   : String

  METHODS
    method init( repo, app )
      package = repo / app
      #{
      # name -> https://github.com/name/name/name
      #   https://github.com/user/repo/.../package -> [https://github.com/user/repo, .../package]
      contingent
        sufficient (package.contains("://"))            # http(s)://...
        sufficient (package.matches_pattern("*@*.*:*")) # git@github.com:...
        which (package.count('/'))
          case 0
            # 'repo' -> 'https://github.com/repo/repo' 'PackMan/repo.rogue'
            # User "repo", repository "repo", project "repo"
            package = "https://github.com/$/$/$" (package,package,package)

          case 1
            # 'user/repo' -> 'https://github.com/user/repo/repo'
            #             -> 'https://github.com/user/repo', 'PackMan/repo.rogue'
            local user = package.before_first( '/' )
            local repo = package.after_first( '/' )
            package = "https://github.com/$/$/$" (user,repo,repo)

          others
            # 'user/repo/[.../]package
            local user = package.before_first( '/' )
            package .= after_first( '/' )
            local repo = package.before_first( '/' )
            local package = package.after_first( '/' )
            package = "https://github.com/$/$/$" (user,repo,package)
        endWhich
      endContingent
      repo = package
      }#

    method display
      @trace package
      @trace repo
      @trace app
      @trace homepage
      @trace version
      @trace url
      @trace sha256
endClass
}#
